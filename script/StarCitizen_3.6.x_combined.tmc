include "tm/target.tmh";
include "sc_bindings.ttm";

int log_level = 1;
int delay_ms = 100;

char isReverseThrust;
char isCameraOn;

alias version = "3.6.0";

int main()
{
	printf("\x0aStartCitizen %s combined Throttle & Joystick\x0a", &version);
	if (log_level > 0)
		printf("... log_level set to %d (change it to 0 to stop logging)\x0a\x0a", log_level);

	Configure(&HCougar, MODE_EXCLUDED);
	Configure(&JoystickF18, MODE_EXCLUDED);
	Configure(&T16000, MODE_EXCLUDED);
	Configure(&T16000L, MODE_EXCLUDED);
	Configure(&LMFD, MODE_EXCLUDED);
	Configure(&RMFD, MODE_EXCLUDED);
	Configure(&TFRPRudder, MODE_EXCLUDED);
	Configure(&TWCSThrottle, MODE_EXCLUDED);
	Configure(&TFRPHARudder, MODE_EXCLUDED);

	if (Init(&EventHandle))
		return 1;

	SetKBRate(50, 33);
	SetKBLayout(KB_ENG);
	SetShiftButton(&Joystick, S4, &Throttle, FLAPU, FLAPD, 0);
	isReverseThrust = 0;
	isCameraOn = 0;

	mapping();
	startup_illumination();
}

int EventHandle(int type, alias o, int x)
{
	if (log_level > 1)
		printf(">> control: %i\x0a", x);

	DefaultMapping(&o, x);
}

// region Private action functions
// REXEC process IDs
int ReverseThrustRIDX = 0x0001;
int AfterburnerRIDX = 0x0002;

int Blink_LED(int led, int blinkrate)
{
	ActKey(KEYON + CHAIN(LED(&Throttle, LED_ONOFF, LED_CURRENT + led), D(blinkrate), LED(&Throttle, LED_ONOFF, LED_CURRENT - led)));
}

int BlinkReverseThrust()
{
	ActKey(KEYON + StopAutoRepeat(ReverseThrustRIDX));
	ActKey(KEYON + REXEC(ReverseThrustRIDX, 800, "Blink_LED(LED5, 400);", RNOSTOP));
}

int BlinkAfterburner()
{
	ActKey(KEYON + StopAutoRepeat(AfterburnerRIDX));
	ActKey(KEYON + REXEC(AfterburnerRIDX, 300, "Blink_LED(LED1, 150);", RNOSTOP));
}
// endregion

// region Function mapping
int mapping()
{

	if (log_level > 0)
		printf("... init mappings:\x0a");

	throttle_maps();
	joystick_maps();
}

int throttle_maps()
{

	if (log_level > 0)
		printf("... add throttle mappings\x0a");

	// keys
	// Full Power
	MapKey(&Throttle, SC, Power_MAX);

	MapKey(&Throttle, MSP, Coupled_Toggle);
	MapKey(&Throttle, MSU, RollLeft_HOLD);
	MapKey(&Throttle, MSD, RollRight_HOLD);
	// sends always the right keystrokes to move the ship forward/backward even if the thrust is currently inverted
	MapKey(&Throttle, MSR, EXEC("if (isReverseThrust) ActKey(KEYON + StrafeForward_HOLD); else ActKey(KEYON + StrafeBackward_HOLD);"));
	MapKeyR(&Throttle, MSR, EXEC("if (isReverseThrust) ActKey(StrafeForward_HOLD); else ActKey(StrafeBackward_HOLD);"));
	MapKey(&Throttle, MSL, EXEC("if (isReverseThrust) ActKey(KEYON + StrafeBackward_HOLD); else ActKey(KEYON + StrafeForward_HOLD);"));
	MapKeyR(&Throttle, MSL, EXEC("if (isReverseThrust) ActKey(StrafeBackward_HOLD); else ActKey(StrafeForward_HOLD);"));

	MapKey(&Throttle, SPDF, CHAIN(Afterburner_HOLD, D(), EXEC("BlinkAfterburner();")));
	MapKeyR(&Throttle, SPDF, EXEC("StopAutoRepeat(AfterburnerRIDX);"));

	MapKey(&Throttle, SPDB, SpaceBrake_HOLD);

	MapKey(&Throttle, BSB, CHAIN(ReverseThrust_Toggle, D(), EXEC("BlinkReverseThrust(); isReverseThrust=1;")));
	MapKeyR(&Throttle, BSB, CHAIN(ReverseThrust_Toggle, D(), EXEC("StopAutoRepeat(ReverseThrustRIDX); isReverseThrust=0;")));

	MapKey(&Throttle, CHF, CountermeasureSelect);

	MapKey(&Throttle, CHB, CountermeasureLaunch);

	MapKey(&Throttle, PSF, Lights_Toggle);
	MapKeyR(&Throttle, PSF, Lights_Toggle);

	MapKey(&Throttle, LTB, LookBack_HOLD);

	MapKey(&Throttle, EFLNORM, CHAIN(Power_Toggle, D(), LEDsON, D(), LEDs86));
	MapKeyR(&Throttle, EFLNORM, CHAIN(Power_Toggle, D(), LEDsON, D(), LEDs43));

	MapKey(&Throttle, EOLMOTOR, Weapons_Toggle);
	MapKey(&Throttle, EORMOTOR, Shields_Toggle);

	// toggling on/off engines and send one 'J' in order to deactivate the initially main thrusters inversion (Probably a bug in SC)
	MapKey(&Throttle, APUON, Engines_Toggle);
	MapKeyR(&Throttle, APUON, Engines_Toggle);

	MapKey(&Throttle, LDGH, LandingGear_DN);
	MapKeyR(&Throttle, LDGH, LandingGear_UP);

	MapKeyIOUMD(&Throttle, FLAPD, 0, 0, 0, 0, MiningMode_Toggle, MiningMode_Toggle);
	MapKeyRIOUMD(&Throttle, FLAPD, 0, 0, 0, 0, MiningMode_Toggle, MiningMode_Toggle);

	MapKey(&Throttle, RDRNRM, AITurrets_Toggle);
	MapKeyR(&Throttle, RDRNRM, AITurrets_Toggle);

	MapKey(&Throttle, APENG, QuantumDrive_DN);
	MapKeyR(&Throttle, APENG, QuantumDrive_UP);

	MapKey(&Throttle, APPAT, CruiseControl_Toggle);
	MapKeyR(&Throttle, APPAT, CruiseControl_Toggle);

	MapKey(&Throttle, EOLIGN, Weapons_Toggle);
	MapKey(&Throttle, EORIGN, Shields_Toggle);

	MapKey(&Throttle, IDLERON, CHAIN(SpaceBrake_HOLD, D(), LED4_ON));
	MapKeyR(&Throttle, IDLERON, LED4_OFF);

	MapKey(&Throttle, CSU, PowerBalanced);
	MapKey(&Throttle, CSD, PowerToEngines_HOLD);
	MapKey(&Throttle, CSR, PowerToShields_HOLD);
	MapKey(&Throttle, CSL, PowerToWeapons_HOLD);

	MapKey(&Throttle, EACON, CHAIN(CameraMode_ON, D(), EXEC("isCameraOn=1;")));
	MapKeyR(&Throttle, EACON, CHAIN(CameraMode_OFF, D(), EXEC("isCameraOn=0;")));

	// axis
	MapAxis(&Throttle, SCX, DX_XROT_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);
	SetSCurve(&Throttle, SCX, 0, 30, 0, 2, 0);

	KeyAxis(&Throttle, SCY, '', AXMAP2(LIST(0, 40, 70), F10, 0, F9));
	MapAxis(&Throttle, SCY, DX_YROT_AXIS, AXIS_REVERSED, MAP_ABSOLUTE);
	SetSCurve(&Throttle, SCY, 0, 40, 0, 2, 0);

	MapAxis(&Throttle, THR_RIGHT, DX_Z_AXIS, AXIS_REVERSED, MAP_ABSOLUTE);
	SetJCurve(&Throttle, THR_RIGHT, 50, 50);

	MapAxis(&Throttle, THR_LEFT, DX_ZROT_AXIS, AXIS_REVERSED, MAP_ABSOLUTE);
	SetJCurve(&Throttle, THR_LEFT, 50, 50);

	MapAxis(&Throttle, THR_FC, DX_SLIDER_AXIS, AXIS_REVERSED, MAP_ABSOLUTE);
	SetJCurve(&Throttle, THR_FC, 50, 50);
}

int joystick_maps()
{

	if (log_level > 0)
		printf("... add joystick mappings\x0a");

	// keys
	MapKey(&Joystick, S3, Modifier_DN);
	MapKeyR(&Joystick, S3, Modifier_UP);

	MapKey(&Joystick, TG2, FireCombined);

	MapKey(&Joystick, H4U, CyclePinned);
	MapKey(&Joystick, H4R, CycleFriendlies);
	MapKey(&Joystick, H4D, CycleSubtarget_HOLD);
	MapKey(&Joystick, H4L, CycleEnemies);
	MapKey(&Joystick, H4P, ReticleFocus_HOLD);

	MapKey(&Joystick, H1U, StrafeUp_DN);
	MapKeyR(&Joystick, H1U, StrafeUp_UP);
	MapKey(&Joystick, H1D, StrafeDown_DN);
	MapKeyR(&Joystick, H1D, StrafeDown_UP);
	MapKey(&Joystick, H1L, StrafeLeft_DN);
	MapKeyR(&Joystick, H1L, StrafeLeft_UP);
	MapKey(&Joystick, H1R, StrafeRight_DN);
	MapKeyR(&Joystick, H1R, StrafeRight_UP);

	// axis
	MapAxis(&Joystick, JOYX, DX_X_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);
	SetSCurve(&Joystick, JOYX, 0, 0, 0, 2, 0);

	MapAxis(&Joystick, JOYY, DX_Y_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);
	SetSCurve(&Joystick, JOYY, 0, 0, 0, 2, 0);
}
// endregion

// region General functions
int startup_illumination()
{

	if (log_level > 0)
		printf("... illuminate throttle\x0a");

	ActKey(KEYON + LEDsOFF);

	int count = 0;
	ActKey(KEYON + CHAIN(D(), LED5_ON, D(delay_ms * count), LED5_OFF,
						 D(), LED4_ON, D(delay_ms * (count + 1)), LED4_OFF,
						 D(), LED3_ON, D(delay_ms * (count + 1)), LED3_OFF,
						 D(), LED2_ON, D(delay_ms * (count + 1)), LED2_OFF,
						 D(), LED1_ON, D(delay_ms * (count + 1)), LED1_OFF,
						 LEDsON, LEDs43));
}
// endregion